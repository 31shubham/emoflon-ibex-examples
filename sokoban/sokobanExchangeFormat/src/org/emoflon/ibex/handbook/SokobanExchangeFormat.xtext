grammar org.emoflon.ibex.handbook.SokobanExchangeFormat

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate sokobanExchangeFormat "http://www.emoflon.org/ibex/handbook/SokobanExchangeFormat"


// Block.................: $ 
// Boulder...............: # 
// Sokoban...............: @ 

// Block on end field....: * 
// Sokoban on end field..: +

// End field.............: . 
// Field.................:   

Board :
	firstRow=Row
;

Row :
	firstEntry=Entry (NEWLINE next=Row)?
;

Entry: 
	End | Normal
;

End: {End}
	(END_FIELD | symbol=EndSymbol) (next=Entry)? 
;

Normal: {Normal}
	(FIELD | symbol=NormalSymbol) (next=Entry)?
;

EndSymbol:
	value=(
		SOKOBAN_ON_END_FIELD |
		BOX_ON_END_FIELD
	)
;

NormalSymbol:
	value=(
		BOULDER | 
		SOKOBAN | 
		BOX
	)
;

Symbol : EndSymbol | NormalSymbol ;

terminal BOULDER: '#';
terminal SOKOBAN: '@';
terminal SOKOBAN_ON_END_FIELD: '+'; 
terminal BOX: '$';
terminal BOX_ON_END_FIELD: '*';
terminal END_FIELD: '.';
terminal FIELD: ' ';

terminal NEWLINE: ('\r'? '\n');